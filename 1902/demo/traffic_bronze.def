include: ../../usaco.def
page: traffic_bronze, basic_frame
    module: cpp

    $struct(sensor) int type, int lo, int hi
    &call open_In
        $call scan_Int, N
        $my struct sensor A[N]
        $for i=0:N
            $my char s[5]
            fscanf(In, "%s %d %d", s, &A[i].lo, &A[i].hi)
            $if s $Eq "on"
                A[i].type=1
            $elif s $Eq "off"
                A[i].type=2
            $else
                A[i].type=0
            # $dump i, A[i].type, A[i].lo, A[i].hi
            
    $my int a1=0, int b1=1000
    $my int a2=0, int b2=1000

    $my int lo_in=0, int hi_in=0
    $my int lo_out=0, int hi_out=0

    /* foreach info points, shrink the range */
    $for i=0:N
        $if A[i].type==1
            lo_in+=A[i].lo
            hi_in+=A[i].hi

            a2 += A[i].lo
            b2 += A[i].hi
        $elif A[i].type==2
            lo_out+=A[i].lo
            hi_out+=A[i].hi

            a2 -= A[i].hi
            b2 -= A[i].lo
            $if a2<0
                a2=0
            $if b2<0
                b2=0
        $elif A[i].type==0
            $my int a = A[i].lo + lo_out - hi_in
            $my int b = A[i].hi + hi_out - lo_in
            $if a1 < a
                a1 = a
            $if b1 > b
                b1 = b

            $if a2 < A[i].lo
                a2 = A[i].lo
            $if b2 > A[i].hi
                b2 = A[i].hi

    &call open_Out
        $print $a1 $b1
        $print $a2 $b2
