include: ../../usaco.def
page: prob3_silver
    module: cpp
    exe: cat t/1.in | ./prob3_silver

    $struct(cow) s[2], int x, int y, int n, int by
    $call scan_Int, N
    $local struct cow * A
    $allocate(N, 0) A
    $for i=0:N
        tn_ret = scanf("%s %d %d", A[i].s, &A[i].x, &A[i].y)
        assert(tn_ret == 3)

    $struct(stop) i, j, n
    $local std::vector<stop> stop_list
    $call get_all_stops

    &call stl_sort, stop_list, struct stop
        return a.n < b.n

    $for i=0:stop_list.size()
        $if stop_list[i].n < 0
            continue
        $call update_A_i
        $call remove_stops_of_i
        $call remove_stops_by_i
        # $call debug_stops

    $call count_children
    $call output

    subcode: update_A_i
        $(set:i=stop_list[i].i)
        A[$(i)].n = stop_list[i].n
        A[$(i)].by = stop_list[i].j
    subcode: remove_stops_of_i
        $for j=i+1:stop_list.size()
            $if stop_list[j].i == stop_list[i].i
                stop_list[j].n = -1
    subcode: remove_stops_by_i
        $for j=i+1:stop_list.size()
            $if stop_list[j].j == stop_list[i].i
                i2 = stop_list[j].i
                j2 = stop_list[j].j
                n2 = stop_list[j].n
                $if A[i2].s[0] == 'E' && A[i2].y > A[j2].y + A[j2].n
                    stop_list[j].n = -1
                $if A[i2].s[0] == 'N' && A[i2].x > A[j2].x + A[j2].n
                    stop_list[j].n = -1

    subcode: debug_stops
        $for j = i:stop_list.size()
            $if stop_list[j].n < 0
                $print "$j:--"
            $else
                $print "$j:(%d,%d-%d) -", stop_list[j].i, stop_list[j].j, stop_list[j].n
        $print

    subcode: count_children
        $my std::vector<int> child_list[N]
        $for i=0:N
            $if A[i].n
                child_list[A[i].by].push_back(i)
                # $print "%d -> %d", A[i].by, i

        $call init_pn_counts
        $for i=0:N
            pn_counts[i] = get_count(child_list, pn_counts, i)

    subcode: output
        $for i=0:N
            $print "%d", pn_counts[i]

    subcode: get_all_stops
        $for i=0:N
            $for j=i+1:N
                $if A[i].s[0] == A[j].s[0]
                    continue
                $local n_i, n_j
                $if A[i].s[0] == 'E' && A[j].x > A[i].x && A[i].y > A[j].y
                    n_i = A[j].x - A[i].x
                    n_j = A[i].y - A[j].y
                $elif A[i].s[0] == 'N' && A[j].y > A[i].y && A[i].x > A[j].x
                    n_i = A[j].y - A[i].y
                    n_j = A[i].x - A[j].x
                $else
                    continue

                $if n_i == n_j
                    continue

                n = std::max(n_i, n_j)
                $if n_i == n
                    stop_list.push_back( (struct stop){i, j, n})
                $else
                    stop_list.push_back( (struct stop){j, i, n})

subcode: init_pn_counts
    $my int pn_counts[N]
    $for i=0:N
        pn_counts[i] = -1

fncode: get_count(std::vector<int> *child_list, int *pn_counts, i): int
    $if pn_counts[i] >= 0
        return pn_counts[i]

    count = 0
    $foreach j in child_list[i]
        count += get_count(child_list, pn_counts, j) + 1
        # $print $i add $j -> $count

    pn_counts[i] = count
    return count
